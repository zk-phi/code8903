#!/usr/bin/fontforge -script

# //// Configurations ////

weight                 = 10
upper_rescale          = 85
upper_l_extra_rescale  = 95
upper_mw_extra_rescale = 90
lower_mw_rescale       = 85
amp_rescale            = 85
at_rescale             = 70
perc_rescale           = 80
dollar_rescale         = 80
question_rescale       = 85
halfwidth              = 625
fullwidth              = 1250

ja_punct_kerning_offset = -400
l_kerning_offset        = 25
r_kerning_offset        = 35
j_kerning_offset        = -50
one_kerning_offset      = -25

roundedmp_rescale_ratio   = 102
roundedmp_baseline_offset = 20

# //// Parse command line args ////

use_expandstroke = "yes"
use_autohint     = "yes"
reuse_jisz8903   = "no"
reuse_roundedmp  = "no"

i = 1
while (i < $argc)
  if ($argv[i] == "--no-expandstroke")
    use_expandstroke = "no"
  endif
  if ($argv[i] == "--no-autohint")
    use_autohint = "no"
  endif
  if ($argv[i] == "--reuse-jisz8903")
    reuse_jisz8903 = "yes"
  endif
  if ($argv[i] == "--reuse-roundedmp")
    reuse_roundedmp = "yes"
  endif
  i = i + 1
endloop

# //// JIS Z 8903 ////

if (reuse_jisz8903 != "yes")

  # ---- Reencode

  Open("JISZ8903-Medium.otf")
  Reencode("unicode")

  # ---- Rescaling

  Print("JISZ8903: Rescaling some glyphs ...")

  Select(0u0030, 0u0039, 0u0041, 0u005A) # 0-9, A-Z
  SelectFewer(0u0049)                    # I
  Scale(upper_rescale, 100, 0, 0)

  Select(0u004C) # L
  Scale(upper_l_extra_rescale, 100, 0, 0)

  Select(0u004D, 0u0057) # M, W
  Scale(upper_mw_extra_rescale, 100, 0, 0)

  SelectSingletons(0u006D, 0u0077) # m, w
  Scale(lower_mw_rescale, 100, 0, 0)

  SelectSingletons(0u0025) # %
  Scale(perc_rescale, 100, 0, 0)

  SelectSingletons(0u0024) # $
  Scale(dollar_rescale, 100, 0, 0)

  SelectSingletons(0u003F) # ?
  Scale(question_rescale, 100, 0, 0)

  SelectSingletons(0u0026) # &
  Scale(amp_rescale, 100, 0, 0)

  SelectSingletons(0u0040) # @
  Scale(at_rescale, 100, 0, 0)

  # ---- Weight

  if (use_expandstroke == "yes")
    Print("JISZ8903: Tweaking weight ...")
    SelectWorthOutputting()
    ExpandStroke(weight, 0, 0, 0, 1)
  endif

  # ---- Monospacify

  Print("JISZ8903: Monospacifying ...")

  # monospacify
  Select(0u0020, 0u007E)          # ASCII
  SetWidth(halfwidth)
  CenterInWidth()

  # monospacify
  SelectWorthOutputting()
  SelectFewer(0u0020, 0u007E)
  SetWidth(fullwidth)
  CenterInWidth()

  # ---- Positioning

  Print("JISZ8903: Positioning glyphs ...")

  SelectSingletons(0u3001, 0u3002) # "、。"
  Move(ja_punct_kerning_offset, 0)
  SetWidth(fullwidth)

  SelectSingletons(0u006c) # "l"
  Move(l_kerning_offset, 0)
  SetWidth(halfwidth)

  SelectSingletons(0u0072) # "r"
  Move(r_kerning_offset, 0)
  SetWidth(halfwidth)

  SelectSingletons(0u006A) # "j"
  Move(j_kerning_offset, 0)
  SetWidth(halfwidth)

  SelectSingletons(0u0031) # "1"
  Move(one_kerning_offset, 0)
  SetWidth(halfwidth)

  # ---- Font Generation

  Print("JISZ8903: Generating temporary font ...")
  Generate("tmp_JISZ8903-Medium.ttf")

  Close()

endif

# //// RoundedM+1m ////

if (reuse_roundedmp != "yes")

  Open("rounded-mplus-1m-regular.ttf")

  # ---- Adjust parameters

  Print("RoundedM+: Adjusting parameters ...")
  SelectWorthOutputting()
  ScaleToEm(800, 200)
  SetOS2Value("WinAscent", 1080)
  SetOS2Value("WinDescent", 120)
  SetOS2Value("HHeadAscent", 958)
  SetOS2Value("HHeadDescent", -203)
  SetOS2Value("HHeadLineGap", 90)
  SetOS2Value("TypoAscent", 800)
  SetOS2Value("TypoDescent", -200)
  SetOS2Value("TypoLineGap", 90)

  # ---- Adjust scale and baseline

  Print("RoundedM+: Adjusting scale and baseline ...")
  Scale(roundedmp_rescale_ratio)
  Move(0, roundedmp_baseline_offset)

  # ---- Adjust weight

  if (use_expandstroke == "yes")
    Print("RoundedM+: Adjusting weight ...")
    ExpandStroke(weight, 0, 0, 0, 1)
  endif

  # ---- Monospacify

  Print("RoundedM+: Monospacifying ...")

  Select(0u0020, 0u007E)          # ASCII
  SetWidth(halfwidth)
  CenterInWidth()

  SelectWorthOutputting()
  SelectFewer(0u0020, 0u007E)
  SetWidth(fullwidth)
  CenterInWidth()

  # ---- Font generation

  Print("JISZ8903: Generating temporary font ...")
  Generate("tmp_rounded-mplus-1m-regular.ttf")

  Close()

endif

# //// Merge Fonts ////

# ---- Copy some glyphs from RoundedM+ to JISZ8903

Print("code8903: Copying some glyphs ...")

Open("tmp_rounded-mplus-1m-regular.ttf")
SelectSingletons(0u005b, 0u005d, 0u007b, 0u007d)
Copy()

Open("tmp_JISZ8903-Medium.ttf")
SelectSingletons(0u005b, 0u005d, 0u007b, 0u007d)
Paste()

# ---- Merge undefined glyphs from RoundedM+ into JISZ8903

Print("code8903: Merging ...")
MergeFonts("tmp_rounded-mplus-1m-regular.ttf")

# ---- Hinting

if (use_autohint == "yes")
  Print("code8903: Hinting ...")
  SelectHintingNeeded()
  AutoHint()
else
  Print("code8903: Removing hints ...")
  SelectWorthOutputting()
  ClearHints()
  ClearInstrs()
  DontAutoHint()
endif

# ---- Font generation

Print("code8903: Generating the font ...")
SelectWorthOutputting()
RoundToInt()
SetFontNames("code8903", "code8903", "code8903")
SetUniqueID(0)
Generate("code8903-Medium.ttf")

Close()
