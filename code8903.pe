#!/usr/bin/fontforge -script

# ---- Configurations

# weight                 = 10
upper_rescale          = 85
upper_mw_extra_rescale = 90
lower_mw_rescale       = 85
amp_rescale            = 85
at_rescale             = 70
perc_rescale           = 80
halfwidth              = 625
fullwidth              = 1250

ja_punct_kerning_offset = -400
l_kerning_offset        = 25
r_kerning_offset        = 35
j_kerning_offset        = -50
one_kerning_offset      = -25

roundedmp_rescale_ratio   = 102
roundedmp_baseline_offset = 20

# ---- Reencode

Open("JISZ8903-Medium.otf")
Reencode("unicode")

# ---- Rescaling

Print("JISZ8903: Rescaling some glyphs ...")

Select(0u0030, 0u0039, 0u0041, 0u005A) # 0-9, A-Z
SelectFewer(0u0049)                    # I
Scale(upper_rescale, 100, 0, 0)

Select(0u004D, 0u0057) # M, W
Scale(upper_mw_extra_rescale, 100, 0, 0)

SelectSingletons(0u006D, 0u0077) # m, w
Scale(lower_mw_rescale, 100, 0, 0)

SelectSingletons(0u0025) # %
Scale(perc_rescale, 100, 0, 0)

SelectSingletons(0u0026) # &
Scale(amp_rescale, 100, 0, 0)

SelectSingletons(0u0040) # @
Scale(at_rescale, 100, 0, 0)

# # ---- Weight
#
# Print("JISZ8903: Tweaking weight ...")
# SelectWorthOutputting()
# ExpandStroke(weight, 0, 0, 0, 1)

# ---- Monospacify

Print("JISZ8903: Monospacifying ...")

# monospacify
Select(0u0020, 0u007E)          # ASCII
SetWidth(halfwidth)

# monospacify
SelectWorthOutputting()
SelectFewer(0u0020, 0u007E)
SetWidth(fullwidth)

# ---- Positioning

Print("JISZ8903: Positioning glyphs ...")

# centering
SelectWorthOutputting()
CenterInWidth()

SelectSingletons(0u3001, 0u3002) # "、。"
Move(ja_punct_kerning_offset, 0)
SetWidth(fullwidth)

SelectSingletons(0u006c) # "l"
Move(l_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u0072) # "r"
Move(r_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u006A) # "j"
Move(j_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u0031) # "1"
Move(one_kerning_offset, 0)
SetWidth(halfwidth)

# ---- Hinting

Print("JISZ8903: Hinting ...")
SelectHintingNeeded()
AutoHint()

# ---- Font Generation

Print("JISZ8903: Generating temporary font ...")
Generate("tmp_JISZ8903-Medium.ttf")

Close()

# ---- Adjust RoundedM+1m to match with JISZ8903

Open("rounded-mplus-1m-regular.ttf")

Print("RoundedM+: Adjusting parameters ...")
SelectWorthOutputting()
ScaleToEm(800, 200)
SetOS2Value("WinAscent", 1080)
SetOS2Value("WinDescent", 120)
SetOS2Value("HHeadAscent", 958)
SetOS2Value("HHeadDescent", -203)
SetOS2Value("HHeadLineGap", 90)
SetOS2Value("TypoAscent", 800)
SetOS2Value("TypoDescent", -200)
SetOS2Value("TypoLineGap", 90)

Print("RoundedM+: Adjusting sacle and baseline ...")
Scale(roundedmp_rescale_ratio)
Move(0, roundedmp_baseline_offset)

# Print("RoundedM+: Adjusting weight ...")
# ExpandStroke(weight, 0, 0, 0, 1)

Print("RoundedM+: Adjusting width ...")
SetWidth(fullwidth)
CenterInWidth()

Print("RoundedM+: Hinting ...")
SelectHintingNeeded()
AutoHint()

Print("JISZ8903: Generating temporary font ...")
Generate("tmp_rounded-mplus-1m-regular.ttf")

Close()

# ---- Merge Fonts

Open("tmp_JISZ8903-Medium.ttf")

Print("code8903: Merging ...")
MergeFonts("tmp_rounded-mplus-1m-regular.ttf")

Print("code8903: Generating the font ...")
SetFontNames("code8903", "code8903", "code8903")
SetUniqueID(0)
Generate("code8903-Medium.ttf")

Close()
