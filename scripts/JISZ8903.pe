#!/usr/bin/fontforge -script

# common configurations
halfwidth = 625
fullwidth = 1250

# rescale ratios
upper_rescale          = 85
upper_l_extra_rescale  = 95
upper_mw_extra_rescale = 90
lower_mw_rescale       = 85
amp_rescale            = 85
at_rescale             = 70
perc_rescale           = 80
dollar_rescale         = 80
question_rescale       = 85

# kerning offsets
ja_punct_kerning_offset = -400
l_kerning_offset        = 25
r_kerning_offset        = 35
j_kerning_offset        = -50
one_kerning_offset      = -25

# ---- Parse command line args

input  = ""
output = ""

use_ss01 = "no"
use_ss02 = "no"

i = 1
while (i < $argc)
  if ($argv[i] == "--ss01")
    use_ss01 = "yes"
  elseif ($argv[i] == "--ss02")
    use_ss02 = "yes"
  elseif (input == "")
    input = $argv[i]
  elseif (output == "")
    output = $argv[i]
  endif
  i = i + 1
endloop

# ---- Reencode

Open(input)
Reencode("unicode")

# ---- Apply style sets

if (use_ss01 == "yes")
  Print("JISZ8903: Applying the style set 01 ...")
  SelectByPosSub("'ss01' Style Set 1 lookup 10 subtable", 1)
  foreach
    dist = $selection
    gsub = GetPosSub("'ss01' Style Set 1 lookup 10 subtable")
    SelectSingletons(gsub[0][2])
    Copy()
    Select(dist)
    Paste()
  endloop
endif

if (use_ss02 == "yes")
  Print("JISZ8903: Applying the style set 02 ...")
  SelectByPosSub("'ss02' Style Set 2 lookup 9 subtable", 1)
  foreach
    dist = $selection
    gsub = GetPosSub("'ss02' Style Set 2 lookup 9 subtable")
    SelectSingletons(gsub[0][2])
    Copy()
    Select(dist)
    Paste()
  endloop
endif

# ---- Rescaling

Print("JISZ8903: Rescaling some glyphs ...")

Select(0u0030, 0u0039, 0u0041, 0u005A) # 0-9, A-Z
SelectFewer(0u0049)                    # I
Scale(upper_rescale, 100, 0, 0)

Select(0u004C) # L
Scale(upper_l_extra_rescale, 100, 0, 0)

Select(0u004D, 0u0057) # M, W
Scale(upper_mw_extra_rescale, 100, 0, 0)

SelectSingletons(0u006D, 0u0077) # m, w
Scale(lower_mw_rescale, 100, 0, 0)

SelectSingletons(0u0025) # %
Scale(perc_rescale, 100, 0, 0)

SelectSingletons(0u0024) # $
Scale(dollar_rescale, 100, 0, 0)

SelectSingletons(0u003F) # ?
Scale(question_rescale, 100, 0, 0)

SelectSingletons(0u0026) # &
Scale(amp_rescale, 100, 0, 0)

SelectSingletons(0u0040) # @
Scale(at_rescale, 100, 0, 0)

# ---- Monospacify

Print("JISZ8903: Monospacifying ...")

# monospacify
Select(0u0020, 0u007E)          # ASCII
SetWidth(halfwidth)
CenterInWidth()

# monospacify
SelectWorthOutputting()
SelectFewer(0u0020, 0u007E)
SetWidth(fullwidth)
CenterInWidth()

# ---- Positioning

Print("JISZ8903: Positioning glyphs ...")

SelectSingletons(0u3001, 0u3002) # "、。"
Move(ja_punct_kerning_offset, 0)
SetWidth(fullwidth)

SelectSingletons(0u006c) # "l"
Move(l_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u0072) # "r"
Move(r_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u006A) # "j"
Move(j_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u0031) # "1"
Move(one_kerning_offset, 0)
SetWidth(halfwidth)

# ---- Font Generation

Print("JISZ8903: Generating temporary font ...")
Generate(output)

Close()
