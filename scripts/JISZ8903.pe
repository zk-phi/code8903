#!/usr/bin/fontforge -script

halfwidth = 625
fullwidth = 1250

# ---- Parse command line args

input  = ""
output = ""

expandstroke = 0
doubleprint  = 0
use_ss01     = "no"
use_ss02     = "no"

i = 1
while (i < $argc)
  if ($argv[i] == "--expandstroke")
    i = i + 1
    expandstroke = Strtol($argv[i])
  elseif ($argv[i] == "--doubleprint")
    i = i + 1
    doubleprint = Strtol($argv[i])
  elseif ($argv[i] == "--ss01")
    use_ss01 = "yes"
  elseif ($argv[i] == "--ss02")
    use_ss02 = "yes"
  elseif (input == "")
    input = $argv[i]
  elseif (output == "")
    output = $argv[i]
  endif
  i = i + 1
endloop

# ---- Open

Open(input)
Reencode("unicode")
SelectWorthOutputting()
ClearInstrs()
UnlinkReference()

# ---- Apply style sets

if (use_ss01 == "yes")
  Print("JISZ8903: Applying the style set 01 ...")
  SelectByPosSub("'ss01' Style Set 1 lookup 10 subtable", 1)
  foreach
    dist = $selection
    gsub = GetPosSub("'ss01' Style Set 1 lookup 10 subtable")
    SelectSingletons(gsub[0][2])
    Copy()
    Select(dist)
    Paste()
  endloop
endif

if (use_ss02 == "yes")
  Print("JISZ8903: Applying the style set 02 ...")
  SelectByPosSub("'ss02' Style Set 2 lookup 9 subtable", 1)
  foreach
    dist = $selection
    gsub = GetPosSub("'ss02' Style Set 2 lookup 9 subtable")
    SelectSingletons(gsub[0][2])
    Copy()
    Select(dist)
    Paste()
  endloop
endif

# ---- Rescaling

Print("JISZ8903: Rescaling some glyphs ...")

Select(0u0030, 0u0039, 0u0041, 0u005A) # 0-9, A-Z
SelectFewer(0u0049)                    # I
Scale(85, 100, 0, 0)
Copy()
PasteWithOffset(15, 0)
RoundToInt()
RemoveOverlap()
RoundToInt()

Select(0u004C) # L
Scale(95, 100, 0, 0)

Select(0u004D, 0u0057) # M, W
Scale(90, 100, 0, 0)

SelectSingletons(0u006D, 0u0077) # m, w
Scale(85, 100, 0, 0)

SelectSingletons(0u0025) # %
Scale(70, 100, 0, 0)
Copy()
PasteWithOffset(10, 0)
RoundToInt()
RemoveOverlap()
RoundToInt()

SelectSingletons(0u0024) # $
Scale(80, 100, 0, 0)

SelectSingletons(0u003F) # ?
Scale(85, 100, 0, 0)

SelectSingletons(0u0026) # &
Scale(85, 100, 0, 0)

SelectSingletons(0u0040) # @
Scale(70, 100, 0, 0)

# ---- Adjust weight

if (expandstroke != 0)
  Print("JISZ8903: Adjusting weight by expanding stoke ...")
  SelectWorthOutputting()
  ExpandStroke(expandstroke, 0, 0, 0, 1)
  RoundToInt()
  RemoveOverlap()
  RoundToInt()
endif

if (doubleprint != 0)
  Print("JISZ8903: Adjusting weight by double printing ...")
  SelectWorthOutputting()
  Print("pass 1 ...")
  Copy()
  PasteWithOffset(doubleprint, 0)
  RoundToInt()
  RemoveOverlap()
  RoundToInt()
  Print("pass 2 ...")
  Copy()
  PasteWithOffset(0, doubleprint)
  RoundToInt()
  RemoveOverlap()
  RoundToInt()
endif

# ---- Monospacify

Print("JISZ8903: Monospacifying ...")

# monospacify
Select(0u0020, 0u007E)          # ASCII
SetWidth(halfwidth)
CenterInWidth()

# monospacify
SelectWorthOutputting()
SelectFewer(0u0020, 0u007E)
SetWidth(fullwidth)
CenterInWidth()

# ---- Positioning

Print("JISZ8903: Positioning glyphs ...")

SelectSingletons(0u3001, 0u3002) # "、。"
Move(-400, 0)
SetWidth(fullwidth)

SelectSingletons(0u006c) # "l"
Move(25, 0)
SetWidth(halfwidth)

SelectSingletons(0u0072) # "r"
Move(35, 0)
SetWidth(halfwidth)

SelectSingletons(0u006A) # "j"
Move(-50, 0)
SetWidth(halfwidth)

SelectSingletons(0u0031) # "1"
Move(-25, 0)
SetWidth(halfwidth)

# ---- Font Generation

Print("JISZ8903: Generating temporary font ...")
Generate(output)

Close()
