#!/usr/bin/fontforge -script

# common configurations
halfwidth = 625
fullwidth = 1250

# ---- Parse command line args

jisz8903  = ""
roundedmp = ""
output    = ""

use_expandstroke = "no"
weight           = 10
use_dottedzero   = "yes"
use_visiblespace = "no"

i = 1
while (i < $argc)
  if ($argv[i] == "--expandstroke")
    use_expandstroke = "yes"
    i = i + 1
    weight = Strtol($argv[i])
  elseif ($argv[i] == "--visiblespace")
    use_visiblespace = "yes"
  elseif ($argv[i] == "--no-dottedzero")
    use_dottedzero = "no"
  elseif (jisz8903 == "")
    jisz8903 = $argv[i]
  elseif (roundedmp == "")
    roundedmp = $argv[i]
  elseif (output == "")
    output = $argv[i]
  endif
  i = i + 1
endloop

# ---- Copy some glyphs from RoundedM+ to JISZ8903

Print("code8903: Copying some glyphs ...")

Open(roundedmp)
SelectSingletons(0u005b, 0u005d, 0u007b, 0u007d)
Copy()

Open(jisz8903)
SelectSingletons(0u005b, 0u005d, 0u007b, 0u007d)
Paste()

# ---- Merge undefined glyphs from RoundedM+ into JISZ8903

Print("code8903: Merging ...")
MergeFonts(roundedmp)

# ---- Adjust weight

if (use_expandstroke == "yes")
  SelectWorthOutputting()
  Print("code8903: Adjusting weight ...")
  ExpandStroke(weight, 0, 0, 0, 1)
  RoundToInt()
  RemoveOverlap()
  RoundToInt()
endif

# ---- Zenkaku space

if (use_visiblespace == "yes")
  Print("code8903: Creating zenkaku space glyph ...")

  # ballot box (☐)
  Select(0u2610)
  Copy()
  Select(0u3000)
  Paste()

  # heavy greek cross (✚)
  Select(0u271a)
  Copy()
  Select(0u3000)
  PasteInto()

  OverlapIntersect()

  RoundToInt()
  RemoveOverlap()
  RoundToInt()
endif

# ---- Dotted zero

if (use_dottedzero == "yes")
  Print("code8903: Adding dot to the zero glyph ...")

  # middle dot (·)
  Select(0u00b7)
  Copy()
  Select(0u0030)
  PasteWithOffset(-halfwidth / 2, 75)

  RoundToInt()
  RemoveOverlap()
  RoundToInt()
endif

# ---- Remove hints

Print("code8903: Removing old hints ...")
SelectWorthOutputting()
ClearHints()
DontAutoHint()

# ---- Font generation

Print("code8903: Generating the font ...")
SetFontNames("code8903", "code8903", "code8903")
SetUniqueID(0)
Generate(output)

Close()
